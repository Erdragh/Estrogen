repositories {}

dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation("net.fabricmc:fabric-loader:${fabric_loader_version}")

    // Fapi
    modApi("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")

    // Architectury
    modApi("dev.architectury:architectury:${architectury_version}") {
        exclude group: "net.fabricmc"
    }

    // Create
    modCompileOnly("com.simibubi.create:create-fabric-${minecraft_version}:${create_fabric_version}+mc${minecraft_version}") {
        exclude group: "net.fabricmc"
    }

    // Trinkets
    modCompileOnly("dev.emi:trinkets:${trinkets_version}") {
        exclude group: "net.fabricmc"
    }

    // Ears
    implementation("com.unascribed:ears-api:${ears_version}") {
        exclude group: "net.fabricmc"
    }

    // Figura
    modCompileOnly("org.figuramc:figura-common-mojmap:${figura_version}") transitive false

    // Mixin Extras
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))
}

architectury {
    common("fabric", "forge")
}

processResources.duplicatesStrategy = DuplicatesStrategy.INCLUDE

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifactId = archives_base_name
            from components.java
        }
    }

    repositories {}
}

sourceSets {
    main {
        // Platform specific generated files
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
            exclude ".cache/**"
            exclude "**/.fabric"
            exclude "**/.forge"

        }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/estrogen.accesswidener")
}
